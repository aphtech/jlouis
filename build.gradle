buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
	}
}

apply plugin: 'nexus'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'org.bitbucket.mwhapples'
archivesBaseName = 'jlouis'
version = '0.0.3-SNAPSHOT'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories { mavenCentral() }
dependencies {
	compile group: 'net.java.dev.jna', name: 'jna', version: '4.1.0'
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
	runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.+'
	testCompile group:'org.testng', name:'testng', version:'6.8.+'
	testCompile group:'org.hamcrest', name:'java-hamcrest', version:'2.+'
}

task tablesZip(type: Zip) {
	from 'src/main/dist/'
	include 'liblouis/tables/*'
	classifier 'tables'
}

test {
	systemProperty "jlouis.library.path", System.getProperty("jlouis.library.path")
	systemProperty "jlouis.data.path", "src/main/dist/"
	useTestNG()
	testLogging {
		events 'skipped', 'failed'
		outputs.upToDateWhen {false}
	}
}

artifacts {
	archives tablesZip
}
			
modifyPom {
	project {
		name 'jlouis'
		description 'Java bindings for LibLouis Braille translator'
		url 'http://bitbucket.org/mwhapples/jlouis'
		scm {
			connection 'scm:hg:http://bitbucket.org/mwhapples/jlouis'
			developerConnection 'scm:hg:https://bitbucket.org/mwhapples/jlouis'
			url 'http://bitbucket.org/mwhapples/jlouis'
		}
		licenses {
			license {
				name 'The Apache License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			}
		}
		developers {
			developer {
				id 'mwhapples'
				name 'Michael Whapples'
				email 'mwhapples@aim.com'
			}
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.0'
}
