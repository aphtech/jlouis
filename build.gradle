apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'org.bitbucket.mwhapples'
archivesBaseName = 'jlouis'
version = '0.0.3-SNAPSHOT'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories { mavenCentral() }
dependencies {
	compile group: 'net.java.dev.jna', name: 'jna', version: '4.1.0'
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
	runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.+'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
	systemProperty "jlouis.library.path", System.getProperty("jlouis.library.path")
	systemProperty "jlouis.data.path", System.getProperty("jlouis.data.path")
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			pom.project {
				name 'jlouis'
				packaging 'jar'
				description 'Java bindings for LibLouis Braille translator'
				url 'http://bitbucket.org/mwhapples/jlouis'
				scm {
					connection 'scm:hg:http://bitbucket.org/mwhapples/jlouis'
					developerConnection 'scm:hg:https://bitbucket.org/mwhapples/jlouis'
					url 'http://bitbucket.org/mwhapples/jlouis'
				}
				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id 'mwhapples'
						name 'Michael Whapples'
						email 'mwhapples@aim.com'
					}
				}
			}
		}
	}
}