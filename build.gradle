buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
	}
}

apply plugin: 'nexus'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'org.bitbucket.mwhapples'
archivesBaseName = 'jlouis'
version = '0.0.4-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories { mavenCentral() }
dependencies {
	//Use maven version ranges as ant-like ranges are incompatible with everything else
	compile group: 'net.java.dev.jna', name: 'jna', version: '4.1.0'
	compile group: 'com.google.guava', name: 'guava', version: '[19.0,)'
	testCompile group: 'javax.xml.bind', name:'jaxb-api', version:'[2.3.1,)'
	testRuntime group: 'org.glassfish.jaxb', name:'jaxb-runtime', version:'[2.3.1,)'
	// block slf4j 1.8 due to jigsaw rewrite and logback not being fully updated -Leon April 27, 2017
	// original version range '[1.7,)'
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
	runtime group: 'ch.qos.logback', name: 'logback-classic', version: '[1.1,)'
	testCompile group:'org.testng', name:'testng', version:'6.11'
	testCompile group:'org.assertj', name:'assertj-core', version:'[3,)'
}

task tablesZip(type: Zip) {
	from 'src/main/dist/'
	include 'org/mwhapples/jlouis/tables/*'
	classifier 'tables'
}

test {
	systemProperty "jlouis.library.path", System.getProperty("jlouis.library.path")
	systemProperty "jlouis.data.path", "src/main/dist/org/mwhapples/jlouis/tables/"
	useTestNG()
	testLogging {
		events 'skipped', 'failed'
		outputs.upToDateWhen {false}
	}
}

artifacts {
	archives tablesZip
}
			
modifyPom {
	project {
		name 'jlouis'
		description 'Java bindings for LibLouis Braille translator'
		url 'http://bitbucket.org/mwhapples/jlouis'
		scm {
			connection 'scm:hg:http://bitbucket.org/mwhapples/jlouis'
			developerConnection 'scm:hg:https://bitbucket.org/mwhapples/jlouis'
			url 'http://bitbucket.org/mwhapples/jlouis'
		}
		licenses {
			license {
				name 'The Apache License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			}
		}
		developers {
			developer {
				id 'mwhapples'
				name 'Michael Whapples'
				email 'mwhapples@aim.com'
			}
		}
	}
}

wrapper {
	gradleVersion = '5.0'
}

apply plugin: 'application'
mainClassName = 'org.mwhapples.jlouis.cli.SimpleTranslate'
applicationDefaultJvmArgs = ["-Djlouis.data.path=${projectDir}/src/main/dist/org/mwhapples/jlouis/tables/"]
install {
	dependsOn 'build'
}

//Gradle does not natively support passing args on the command line, nor changing the main class
//http://forums.gradle.org/gradle/topics/how_can_i_provide_command_line_args_to_application_started_with_gradle_run
run {
    if ( project.hasProperty("appArgs") ) {
		if (appArgs.startsWith("[")) {
			//Eg ["--arg", "value", "--arg2"]
			args Eval.me(appArgs);
		} else {
			args project.appArgs.tokenize()
		}
    }
}
